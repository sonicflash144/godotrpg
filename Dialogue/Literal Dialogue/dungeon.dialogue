~ start
do Events.set_flag("met_princess")
Princess: Oh hey there! [#portrait=happy_closed_mouth]
Princess: WOWWW, are you a real warrior? [#portrait=surprise]
- Yes
	Princess: That's so cool. Like, this might be THE COOLEST moment of my life. [#portrait=happy_teeth]
- No
	Princess: Hah, well, you sure look the part with that big sword. [#portrait=wink]
Princess: I heard only the bravest warriors end up here. [#portrait=happy_closed_mouth]
Princess: Not that I'm not brave, of course. [#portrait=surprise]
Princess: I'm... a deadly prisoner, just like you! Yeah! [#portrait=neutral]
Princess: May I join you on your adventures? [#portrait=happy]
- Yes
	Princess: Oh my gosh, really? Awesome! [#portrait=happy_eyes_closed]
- No
	Princess: Aww, come on. Pretty please? [#portrait=sad_closed_mouth]
Princess: I promise you won't regret it. I'm already kind of a fighter myself. [#portrait=smirk]
Princess: Here, try hitting me. [#portrait=happy]
=> END

~ ignore
do Events.set_flag("met_princess")
Princess: HEY! Aren't you going to say hello? [#portrait=angry]
- Sorry
	Princess: Sorry doesn't cut it! [#portrait=angry_closed_mouth]
- I'm busy
	Princess: Well, you're also a jerk! [#portrait=angry_closed_mouth]
Princess: I could be a huge asset to your adventures and you tried to walk right past me. [#portrait=angry_closed_mouth]
Princess: I know you think you need that big sword a lot more than a girl like me. [#portrait=neutral]
Princess: But look, I'm already kind of a fighter myself. [#portrait=smirk]
Princess: Here, try hitting me. [#portrait=happy]
=> END

~ talked_loop
Princess: Here, try hitting me. [#portrait=happy]
=> END

~ hit
do Events.set_flag("hit_princess")
Princess: OWWW OW OWWW OW OW! [#portrait=angry_eyes_closed]
if not Events.get_flag("met_princess"):
	do Events.set_flag("met_princess")
	Princess: WHAT THE HECK, DUDE? [#portrait=angry]
	Princess: We haven't even talked yet, and you've already decided I'm a nuisance or something? [#portrait=angry]
	Princess: What's your problem? [#portrait=angry]
	- Sorry
		Princess: Apology, like, 10 percent accepted. [#portrait=angry_closed_mouth]
	- You are a nuisance
		Princess: ... [#portrait=suspicious]
	Princess: You know, I could be a huge asset to your adventures. [#portrait=neutral]
	Princess: I know you think you need that big sword a lot more than a girl like me. [#portrait=neutral]
	Princess: But I'll show you the kind of fighter I am! [#portrait=smirk]
else:
	Princess: Ow... don't worry, I'm okay. [#portrait=wince]
Princess: Let's get going. [#portrait=neutral]
Princess: The life of a brave warrior waits for no one! [#portrait=happy_teeth]
do remove_dialogue_barrier()
do Events.dialogue_movement.emit("enter_ghost_room")
set Events.num_party_members = 2
do wait(0.5)
=> END

~ hit_loop
Princess: DUDE! You can stop hitting me now. We're on the same team. [#portrait=angry]
=> END

~ ghost_room
do Events.set_flag("combat_room_2")
Princess: AHHH! OH MY GOODNESS ARE THOSE MONSTERS??? [#portrait=shocked]
do Events.dialogue_movement.emit("exit_ghost_room")
set Events.num_party_members = 1
do wait(1.5)
do combat_lock_signal()
do wait(0.5)
=> END

~ fled
do Events.set_flag("princess_apology")
Princess: I'm sorry for running away earlier. [#portrait=sad_closed_mouth]
Princess: I just got so scared... [#portrait=sad_eyes_closed]
Princess: I've never actually seen a monster up close before. [#portrait=sad]
Princess: It was silly of me to play pretend like this. I'm no hero. [#portrait=sad]
- Forgive
	Princess: Aww, you shouldn't. [#portrait=embarassed_happy]
- Do not
	Princess: I understand. [#portrait=downcast]
Princess: I'll prove myself soon, just you wait! [#portrait=determined]	
=> END

~ fled_loop
Princess: I'll prove myself soon, just you wait! [#portrait=determined]
=> END

~ fled_visited_door
=>< fled
=> door_help
=> END

~ door_help
Princess: What? There's a door up ahead you need my help with? [#portrait=surprise]
Princess: I'll be right there! [#portrait=happy_closed_mouth]
do Events.dialogue_movement.emit("enter_door_room")
do wait(0.5)
do Events.set_flag("princess_door_ready")
=> END

~ door
Princess: Wow, cool room. [#portrait=surprise]
Princess: Okay, I guess I just... stand on one of these? [#portrait=surprise_slight]
do Events.dialogue_movement.emit("stand_door_button")
do wait(0.5)
=> END

~ door_opened
Princess: Woah, nice job! [#portrait=surprise_happy]
Princess: We make a pretty good team, huh? [#portrait=happy_teeth]
Princess: Come on, let's get going! [#portrait=happy_closed_mouth]
do set_princess_follow_state() 
=> END

~ door_description
The door is locked.
Seems like you might need some help to open it.
=> END