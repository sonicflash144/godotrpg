~ wall
if not Events.get_flag("checked_wall"):
	It's a wall. Unfortunately doesn't look very breakable.
	do Events.set_flag("checked_wall", true)
else:
	...Still a wall.
=> END

~ blood
It's an ancient-looking bloodstain. Doesn't seem like the guards around here are very merciful.
=> END

~ vent
It's a rusty-looking vent. Doesn't seem to be circulating any air anymore.
- Peer inside
	You see a few dust bunnies that have formed a small, sad-looking civilization.
	They seem to be worshipping a puddle of unidentifiable grey liquid.
- Do not
=> END

~ cell_door
It's a prison door made of solid steel.
- Bang on it => start
- Do not
=> END

~ start
do Events.dialogue_movement.emit("guard_enter")
do wait(1.2)
do open_prison_door()
do wait(0.5)
Guard: HEY! I was literally right outside your cell. Have some patience. [#portrait=neutral]
Guard: Here's your daily food ration. [#portrait=neutral]
Guard: You know the drill. Any funny business, and I'll— [#portrait=neutral]
do alarm_animation()
do wait(1)
Guard: Ugh, what is it now? [#portrait=neutral]
Guard: You. STAY PUT. [#portrait=neutral]
do Events.dialogue_movement.emit("guard_leave")
do wait(2)
=> END

~ paper
"GUIDLINES FOR PRISON GUARDS"
"Be loud. Be intimidating. NEVER leave weapons unattended."
=> END

~ sandwich
It's a sandwich, slightly squished from being hastily dropped on the floor. Eat it?
- Eat it
	do Events.set_flag("ate_sandwich")
	do eat_sandwich()
	You ate the sandwich. The bread is stale, but it's the best thing you've eaten in a while.
- Do not
=> END

~ generic_door
You can hear the muffled sounds of a commotion going on outside. 
Listen through the crack in the door?
- Listen
	"...[color=yellow]THE[/color] prisoner... crazy... I think... up ahead..."
	"...Wasn't he the one who... dangerous..."
- Do not
=> END

~ sword
It's a sword left behind by one of the guards. Take it?
- Take it => guard_return
- Not yet
=> END

~ guard_return
do take_sword()
You picked up the sword. You can now use \[X\] to attack.
do Events.dialogue_movement.emit("guard_return")
do wait(0.5)
do open_generic_door()
do wait(1.5)
do player_face_guard()
Guard: HEY! I told you to stay— [#portrait=neutral]
if Events.get_flag("ate_sandwich"):
	Guard: ...Did you seriously eat my sandwich? My mom made that for me, you know. [#portrait=neutral]
Guard: Wait, is that my sword? Woah, woah, calm down. [#portrait=neutral]
Guard: You're not gonna hurt me or anything, right? [#portrait=neutral]
Guard: I was a nice guard! I remembered to bring you your rations... most days of the week. [#portrait=sweat]
Guard: Look, between you and me, I never wanted to work this job. [#portrait=neutral]
Guard: The [color=yellow]KING[/color] forced me into this position in some... creative ways. [#portrait=neutral]
Guard: So how about this. I let you go, and you, uh, don't stab me. Yeah? [#portrait=neutral]
- Attack
- Do not
do Events.dialogue_movement.emit("guard2_enter")
do wait(2)
do guard_face_guard2()
Guard2: We got him! [#portrait=neutral]
Guard: Took you guys long enough! This lunatic nearly killed me! [#portrait=neutral]
Guard2: Please, this guy looks like a wimp compared to [color=yellow]THE[/color] prisoner. [#portrait=neutral]
Guard2: While you were messing around, we were preventing his third escape attempt this week. [#portrait=neutral]
Guard: Did you relocate him to a higher security cell? [#portrait=neutral]
Guard2: No, we decided on a more... permanent solution. [#portrait=neutral]
Guard: You KILLED him?? [#portrait=neutral]
Guard2: No. Well... considering where he is now, we might as well have. [#portrait=neutral]
Guard: Ahh, I get it. Smart. [#portrait=neutral]
Guard2: We really should have stuck him there the minute we first captured him. [#portrait=neutral]
Guard: You don't seriously believe the [color=yellow]PRINCESS[/color] was in any real danger. [#portrait=neutral]
Guard2: SHH! Not in front of the prisoner! [#portrait=neutral]
do guard_face_player()
Guard: Oops. [#portrait=sweat]
Guard2: ... [#portrait=neutral]
Guard2: Well, it's too late now. What to do, what to do? [#portrait=neutral]
Guard2: Oh, I have a great idea. Let's give [color=yellow]THE[/color] prisoner a friend down there to keep him company. [#portrait=neutral]
Guard2: Now now, don't look so terrified. We'll even let you keep that little sword you've picked up. [#portrait=neutral]
Guard2: You'll need it to survive... down in the [color=yellow]DUNGEON[/color]. [#portrait=neutral]
Guard: Hahahahah! [#portrait=neutral]
Guard: Sorry. It's just... "surviving"... in the [color=yellow]DUNGEON[/color].... ahahahah! [#portrait=neutral]
Guard2: If you can make it to the exit alive, you win your freedom! [#portrait=neutral]
Guard: Hahahahah! [#portrait=neutral]
Guard: Sorry. It's just... "alive"... in the [color=yellow]DUNGEON[/color].... ahahahah! [#portrait=neutral]
Guard2: So, how does that sound? [#portrait=neutral]
- Good
	Guard2: I'm glad you're excited, brave warrior. [#portrait=neutral]
- Bad
	Guard2: Well, good think we don't actually care what you think. [#portrait=neutral]
	Guard: Don't worry, it'll be over before you know it. [#portrait=neutral]
Guard2: We'll take you down there without further delay. [#portrait=neutral]
do transition_to_dungeon()
do wait(1)
=> END